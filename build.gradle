plugins {
    id 'maven-publish'
    id 'java'
    id 'com.google.cloud.artifactregistry.gradle-plugin' version '2.1.5'
    id 'jacoco' // Add JaCoCo plugin
}

group = 'com.erebos.flu'
version = '1.0.39'

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

ext {
    guavaVersion = '32.1.3-jre'
    junit5Version = '5.11.0'
    hamcrestVersion = '2.2'
    log4j2Version = '2.17.1'
}

dependencies {
    // Guava dependency
    implementation "com.google.guava:guava:$guavaVersion"

    // MapStruct
    implementation group: 'org.mapstruct', name: 'mapstruct', version: '1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    implementation 'org.testng:testng:7.7.0'

    // JUnit 5 testing dependencies
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit5Version"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit5Version"

    // Hamcrest for assertions in tests
    testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.release = 20
}

// JaCoCo configuration
jacoco {
    toolVersion = "0.8.10" // Specify the JaCoCo version
}

jacocoTestCoverageVerification {
    violationRules {
        // Rule to enforce minimum coverage of 85%
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }
        }

        // Rule to limit complexity
        rule {
            element = 'METHOD'
            limit {
                counter = 'COMPLEXITY'
                value = 'TOTALCOUNT'
                maximum = 5 // Enforce max complexity
            }
        }

        // Rule to limit number of lines per method
        rule {
            element = 'METHOD'
            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 10 // Enforce max lines per method
            }
        }
    }
}

// Publishing configuration for Maven
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url = uri("path/to/your/local/maven-repo") // Define your repository
        }
    }
}
